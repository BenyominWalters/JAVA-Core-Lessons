---


---

<h1 id="solutions-paint-program">02.1 Solutions: Paint Program</h1>
<p>Here are possible solutions for the practice challenges. There are many ways of solving any problem in Java, and your answer may vary, or even be better!</p>
<p>This code uses the default permissions. Later we’ll learn about encapsulation and add protections to secure our program.</p>
<p>In this basic program I created a few classes.</p>
<ol>
<li><strong>PaintingProgram</strong> This class is the main class that runs the application.</li>
<li><strong>Button</strong> This class contains the information for creating buttons, showing them on the screen, and registering clicks.</li>
<li><strong>CollisionBox</strong> Each button has a collision box that detects if the mouse is over the box. I could have just made this a method of the button, but I want to be able to use the collision box for other applications, because it is a very common element. This way I can easily add its functionality to other projects.</li>
<li><strong>ColorButton</strong> This class extends the Button class and has special functions for using color objects to set the draw color.</li>
<li><strong>StrokeButton</strong> This class extends the Button class and has special functions for changing the stroke size, and drawing its icon (“plus”, or “minus sign”). This could also be refactored to a regular Button with an icon.</li>
<li><strong>Color</strong> I created this class so that I can easily send colors as rgb values. Since these are three numbers, it is easiest to return them as an object. Otherwise, I would need a separate method to return each color.</li>
</ol>
<p>There are many ways to build on and improve this program. For example, adding the ability to draw shapes and fill them with a “paint bucket”. Also it would be nice to add a button to save the drawing as an image or pdf.</p>
<p>Here is the code.</p>
<h3 id="paintprogram-class">PaintProgram Class</h3>
<pre><code>Button eraseButton; // turns on eraser
Button clearButton; // clears screen

boolean eraseMode = false; // Makes stroke bigger when erasing

StrokeButton plusButton; // Increases stroke size
StrokeButton minusButton; // Decreases stroke size
int currentStroke = 1; // Default line size is 1

ColorButton[] colorButtons = new ColorButton[10]; // Make 10 ColorButtons for swatch

Color currentColor; // Color object to hold selected color
// variables to store each of the three values of color r,g,b default is black
int r = 0; 
int g = 0;
int b = 0;

void setup() {
  size(600, 600);
  background(255);
  
  // Give each button a size and position
  eraseButton = new Button(48, 14, 22, 22); 
  clearButton = new Button(20, 14, 22, 22);
  // Add icons for some buttons
  eraseButton.setIcon("erasorIcon.png");
  clearButton.setIcon("clearIcon.png");
 
  plusButton = new StrokeButton(48, 40, 22, 22);
  minusButton = new StrokeButton(20, 40, 22, 22);
  // these methods draws the icon, instead of using an image
  plusButton.setType("plus"); 
  minusButton.setType("minus"); 

  // create each of the color button with size and position
  for (int i = 0; i &lt; colorButtons.length; i++) {
    int size = 50;
    int yOffset = 50 + i * size;
    colorButtons[i] = new ColorButton(20, 20 + yOffset, size, size);
  }
  // set the color of each button
  colorButtons[0].setColor(0, 0, 0); // black
  colorButtons[1].setColor(255, 0, 0); // red
  colorButtons[2].setColor(0, 255, 0); // green
  colorButtons[3].setColor(0, 0, 255); // blue
  colorButtons[4].setColor(255, 255, 0); // yellow
  colorButtons[5].setColor(0, 255, 255); // cyan
  colorButtons[6].setColor(255, 0, 255); // magenta
  colorButtons[7].setColor(255, 130, 0); // orange
  colorButtons[8].setColor(64, 130, 109); // green/blue
  colorButtons[9].setColor(90, 45, 0); // brown
} // end setup method

void draw() {

  // swatch background so you can't paint over the button panel
  fill(255);
  noStroke();
  rect(0, 0, 80, height);
  
  // display the current color and line-size in boxes
  fill(r, g, b);
  stroke(0);
  square(width / 2 - 52, 10, 50);
  fill(255);
  stroke(0);
  square(width / 2 + 2, 10, 50);
  fill(r, g, b);
  circle(width / 2 + 27, 35, currentStroke);
  
  // draw all of the buttons to the screen
  eraseButton.show();
  clearButton.show();

  plusButton.show();
  minusButton.show();
  
  // draw each color button
  // check if each color button is clicked to set current color
  // The color check could be in its own for loop
  for (ColorButton button : colorButtons) {
    button.show();
    if (button.clicked()) {
      currentColor = button.myColor;
      r = currentColor.r;
      g = currentColor.g;
      b = currentColor.b;
      eraseMode = false;
    }
  } // end of for loop
  
  // update the stroke size if the stroke buttons are clicked
  updateStroke(); 
  
  // draw a line if the mouse is pressed, with current color and stroke
  if (mousePressed) {
    pushStyle();
    stroke(r, g, b);
    strokeWeight(currentStroke);
    line(pmouseX, pmouseY, mouseX, mouseY);
    popStyle();
  }

  // erase mode settings
  if (eraseButton.clicked()) {
    r = 255;
    g = 255;
    b = 255; 
    eraseMode = true;
    currentStroke = 30;
  }
  
  // reset screen if clear button is clicked
  if (clearButton.clicked()) {
    background(255);
  }
} // end draw method

// Increase or decrease the size of the line stroke
// Keep the maximum and minimum size in limits
// Allow for bigger eraser than line
void updateStroke() {

  if (plusButton.clicked()) {
    currentStroke++;
  } else if (minusButton.clicked()) {
    currentStroke--;
  }

  if (currentStroke &lt; 1 &amp;&amp; eraseMode == false) {
    currentStroke = 1;
  } else if (currentStroke &gt; 20 &amp;&amp; eraseMode == false) {
    currentStroke = 20;
  } else if (eraseMode &amp;&amp; currentStroke &gt; 50) {
    currentStroke = 50;
  }
} // end updateStroke method
</code></pre>
<h4 id="button-class">Button Class</h4>
<pre><code>class Button {
  // the location and size of the button
  float x;
  float y;
  float w;
  float h;
  CollisionBox myCB; // each button has a collision box

  // Each button can have a color as its fill
  Color myFill;
  // Each button can have an icon image
  PImage myIcon;

  // was the button was pressed
  boolean pressed = false;
  // was the button pressed and then released
  boolean clicked = false;
  
  // Button Constructor takes location and size, and sets its collision box to the same size
  // Default color is gray
  Button(float x, float y, float w, float h) {
    this.x = x;
    this.y = y;
    this.w = w;
    this.h = h;
    myCB = new CollisionBox(x, y, w, h);
    myFill = new Color(200, 200, 200);
  }

  // draws a rectangle with button's size, location, fill, and icon (if any)
  void show() {
    noStroke();
    fill(myFill.r, myFill.g, myFill.b);
    if (myCB.mouseOverBox()) {
      rect(x-5, y-10, w+10, h+10);
      drawIcon(x-5, y-10, w+10, h+10);
    } else {
      rect(x, y, w, h);
      drawIcon(x, y, w, h);
    }
  }
  
  // Setter method to change button fill color
  void setFill(int r, int g, int b) {
    myFill = new Color(r, g, b);
  }

  // detect if the button was clicked
  boolean clicked() {
    if (myCB.mouseOverBox() &amp;&amp; mousePressed) {
      pressed = true;
    }
    if (pressed == true &amp;&amp; !mousePressed) {
      clicked = true;
      pressed = false;
    } else {
      clicked = false;
    }
    return clicked;
  }
  
  // Setter method for button icon
  void setIcon(String iconLocation) {
    myIcon = loadImage(iconLocation);    
  }
  
  // Method to set icon size and display it on button
  void drawIcon(float x, float y, float w, float h){
    if(myIcon != null){
       image(myIcon, x, y, w, h);
    }
  }
}
</code></pre>
<h4 id="collisionbox-class">CollisionBox Class</h4>
<pre><code>class CollisionBox {
  // A collision box has a location and a size
  float x;
  float y;
  float w;
  float h;

  // Constructor for collision box needs location and size
  CollisionBox(float x, float y, float w, float h) {
    this.x = x;
    this.y = y;
    this.w = w;
    this.h = h;
  }

  // detects if mouse is over box
  boolean mouseOverBox() {

    float left = x;
    float right = x + w;
    float top = y;
    float bottom = y + h;

    // If any of these are true, the mouse is not over the box 
    // If this is false, the mouse IS over the box
    return !(mouseX &lt; left || mouseX &gt; right || mouseY &lt; top || mouseY &gt; bottom);
  }

  // detects if two collision boxes touch
  boolean intersects (CollisionBox otherCollisionBox) {
    float left = x;
    float right =  x + w;
    float top = y;
    float bottom = y + h;

    float oleft = otherCollisionBox.x;
    float oright = otherCollisionBox.x + otherCollisionBox.w;
    float otop = otherCollisionBox.y;
    float obottom = otherCollisionBox.y + otherCollisionBox.h;

    // If any of these are true, they are not intersecting,
    // if this is not true, they are intersecting
    return !(left &gt;= oright ||
      right &lt;= oleft ||
      top &gt;= obottom ||
      bottom &lt;= otop);
  }
}
</code></pre>
<h4 id="colorbutton-class">ColorButton Class</h4>
<pre><code>class StrokeButton extends Button {
  // A stroke button inherits everything from a regular button
  // In addition it has a type of "plus" or "minus"
  String myType = "minus";
  
  // Stroke button constructor sends info to Button (super), and sets its fill
  StrokeButton(float x, float y, float w, float h) {
    super(x, y, w, h);
    setFill(200, 200, 200);
  }

  // extends the show function of a regular button to include drawing the icon
  // can replace with an image icon using default button methods
  void show() {
    super.show();  
     if (myCB.mouseOverBox()) {
      rect(x-5, y-10, w+10, h+10);
      drawIcon(x-5, y-10, w+10, h+10);
    } else {
      rect(x, y, w, h);
      drawIcon(x, y, w, h);
    }
  }

  // Setter method to set the button type to "plus" or "minus"
  void setType(String type) {
    if (type.equals("plus")) {
      myType = type;
    } else if (type.equals("minus")) {
      myType = type;
    } else {
      println("Invalid Stroke Button Type. Enter PLUS or MINUS");
    }
  }
  
  // draws the plus or minus icon
  void drawIcon(float x, float y, float w, float h) {
    if (myType.equals("minus")) {
      pushStyle();
      stroke(0);
      strokeWeight(3);
      line(x+5, y+h/2, x+w-5, y+h/2);
      popStyle();
    } else if (myType.equals("plus")){
      pushStyle();
      stroke(0);
      strokeWeight(3);
      line(x+5, y+h/2, x+w-5, y+h/2);
      line(x+w/2, y+5, x+w/2, y+h-5);
      popStyle();
    }
  }
}
</code></pre>
<h4 id="color-class">Color Class</h4>
<pre><code>class Color{
  int r;
  int g;
  int b;
  
  Color(int r, int g, int b) {
    this.r = r;
    this.g = g;
    this.b = b;
  }
}
</code></pre>
<blockquote>
<p>Written with <a href="https://stackedit.io/">StackEdit</a>.</p>
</blockquote>

