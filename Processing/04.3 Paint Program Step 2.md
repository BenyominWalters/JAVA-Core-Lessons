---


---

<h1 id="paint-program-step-2">Paint Program Step 2</h1>
<p>We have a basic paint program working. Now let’s add some color.</p>
<p>We need:</p>
<ol>
<li>A swatch of color buttons running down the side</li>
<li>Each button should be filled with its color</li>
<li>Clicking on each button should change the line color accordingly</li>
</ol>
<p>Let’s get cracking!</p>
<p>Let’s start by creating 8 color buttons and displaying them down the side. We could do this manually, like we did for the thickness buttons, but since they are so similar, let’s just use an array and a for loop.</p>
<h4 id="paintprogram">PaintProgram</h4>
<pre><code>int w = 50;
int thickness = 1;

Button thinButton;
Button thickButton;

// Create an array of 8 Buttons
Button[] colorButtons = new Button[8];

void setup() {
  size(600, 600);
  background(255);

  thinButton = new Button(x, y, w, w);
  thickButton = new Button(x, y + w, w, w);
  
  // Initialize 8 new Buttons and put each in the array
  // Each button gets an offset so it will be below the previous button
  for (int i = 0; i &lt; colorButtons.length; i++) {
    int offset = y + (w * (i+ 2));
    colorButtons[i] = new Button (x, y + offset, w, w);
  }
} // end of setup method

void draw() {
  thinButton.show();
  thickButton.show();

  // Draw dots for thin and thick buttons
  pushStyle();
  strokeWeight(3);
  point(x + w/2, y + w/2);

  strokeWeight(10);
  point(x + w/2, y + w + w/2);
  popStyle();

  // show each of the color buttons
  for (Button button : colorButtons) {
    button.show();
  }

  if (mousePressed) {
    pushStyle();
    strokeWeight(thickness);
    line(pmouseX, pmouseY, mouseX, mouseY);
    popStyle();
  }
} // end of draw method

void mouseClicked() {
  if (thinButton.mouseOver()) {
    thickness = 1;
  } else if (thickButton.mouseOver()) {
    thickness = 5;
  }
} // end of mouseClicked method
</code></pre>
<p>Notice how we get all of the functionality of our Button class (like the highlighting) automatically for each button. This makes our PaintProgram simple and clean, yet we get all of the Button methods we need!</p>
<p>We now have an empty swatch of 8 blank buttons under the thickness buttons. Let’s give the buttons colors.</p>
<p>To do this, let’s modify our Button class so that buttons can have a color. Then, we just need to set the color of each button’s fill. After that, we can also use the button’s color to set our line color. Let’s go!</p>
<p>Let’s start by making a Color class to hold our three rgb values. It may be overkill, but it will make certain things easier, and is good practice working with classes and objects.</p>
<p>All our color class needs is three values for red, green, and blue values.</p>
<h4 id="color">Color</h4>
<pre><code>class Color {
  int r;
  int g;
  int b;
  
  Color(int r, int g, int b){
    this.r = r;
    this.g = g;
    this.b = b;
  }
}
</code></pre>
<p>Now let’s give our button variables for a color, and rgb values.<br>
We need to set these values in our constructor.<br>
Then, use these values to change the fill.</p>
<h4 id="button">Button</h4>
<pre><code>class Button {
  int myX;
  int myY;
  int myWidth;
  int myHeight;
  
  Color myColor;
  int r;
  int g;
  int b;

  Button(int x, int y, int w, int h) {
    myX = x;
    myY = y;
    myWidth = w;
    myHeight = h;
  }

  void show() {
	fill(r, g, b);
    highlight();
    rect(myX, myY, myWidth, myHeight);
  }

  void highlight() {
    if (mouseOver()) {
      fill(150, 150, 150);
    } else {
      fill(255, 255, 255);
    }
  }

  boolean mouseOver() {
    boolean over;
    if (mouseX &gt; myX &amp;&amp; mouseX &lt; myX + myWidth &amp;&amp; mouseY &gt; myY &amp;&amp; mouseY &lt; myY + myHeight) {
      over = true;
    } else {
      over = false;
    }
    return over;
  }
}
</code></pre>
<p>This is great, but we are creating all of our buttons with a white color by default. We need a way to set a button’s color. We could require this in the constructor, but it can get confusing if there are too many values to set when creating an object. Instead, we’ll create a setter method to set a button’s color values.</p>
<p>In this step, we will:</p>
<ol>
<li>Add a <strong>setter</strong> method to our Button class to set its color.</li>
<li>Add a <strong>getter</strong> method to get our button’s color.</li>
<li>Change our highlight function to behave differently for colored buttons than white buttons.</li>
<li>This requires adding highlight variable to our class and constructor.</li>
</ol>
<h4 id="button-1">Button</h4>
<pre><code>class Button {
  int myX;
  int myY;
  int myWidth;
  int myHeight;

  Color myColor;
  int r;
  int g;
  int b;
  int highlight; // value for lightening color when mouseOver

  Button(int x, int y, int w, int h) {
    myX = x;
    myY = y;
    myWidth = w;
    myHeight = h;

    myColor = new Color(255, 255, 255);
    r = myColor.r;
    g = myColor.g;
    b = myColor.b;
    highlight = 100;
  }

  void show() {
    fill(r, g, b); // set the fill to the Button color
    highlight();
    rect(myX, myY, myWidth, myHeight);
  }

  // Method for setting a button's color
  void setColor(int r, int g, int b) {
    myColor.r = r;
    myColor.g = g;
    myColor.b = b;

    this.r = r;
    this.g = g;
    this.b = b;
  }
  
  // This method returns a Color, so its return type is Color!
  Color getColor(){
    return myColor;
  }

  void highlight() {
    if (mouseOver() &amp;&amp; r &gt; 254 &amp;&amp; g &gt; 254 &amp;&amp; b &gt; 254) {
      fill(150, 150, 150);
    } else if (mouseOver()) {
      fill(r + highlight, g + highlight, b + highlight);
    }
  }

  boolean mouseOver() {
    boolean over;
    if (mouseX &gt; myX &amp;&amp; mouseX &lt; myX + myWidth &amp;&amp; mouseY &gt; myY &amp;&amp; mouseY &lt; myY + myHeight) {
      over = true;
    } else {
      over = false;
    }
    return over;
  }
}
</code></pre>
<p>Now that that is all setup, we just need to set each button’s color value.</p>
<p>Unfortunately, we need to do this manually for each button, because we are picking each color without following a pattern.</p>
<p>We do this by calling the <strong>setColor</strong> method in <strong>setup</strong> after creating the buttons.</p>
<h4 id="paint-program">Paint Program</h4>
<pre><code>int x = 10;
int y = 10;
int w = 50;
int thickness = 1;

Button thinButton;
Button thickButton;

// Create an array of 8 Buttons
Button[] colorButtons = new Button[8];

void setup() {
  size(600, 600);
  background(255);

  thinButton = new Button(x, y, w, w);
  thickButton = new Button(x, y + w, w, w);
  
  // Initialize 8 new Buttons and put each in the array
  // Each button gets an offset so it will be below the previous button
  for (int i = 0; i &lt; colorButtons.length; i++) {
    int offset = y + (w * (i+ 2));
    colorButtons[i] = new Button (x, y + offset, w, w);
  }
  
  colorButtons[0].setColor(255, 0, 0);
  colorButtons[1].setColor(0, 0, 255);
  colorButtons[2].setColor(0, 255, 0);
  colorButtons[3].setColor(255, 255, 0);
  colorButtons[4].setColor(255, 0, 255);
  colorButtons[5].setColor(0, 255, 255);
  colorButtons[6].setColor(102, 51, 0);
  colorButtons[7].setColor(0, 0, 0);
} // end of setup method

void draw() {
  thinButton.show();
  thickButton.show();

  // Draw dots for thin and thick buttons
  pushStyle();
  strokeWeight(3);
  point(x + w/2, y + w/2);

  strokeWeight(10);
  point(x + w/2, y + w + w/2);
  popStyle();

  // show each of the color buttons
  for (Button button : colorButtons) {
    button.show();
  }

  if (mousePressed) {
    pushStyle();
    strokeWeight(thickness);
    line(pmouseX, pmouseY, mouseX, mouseY);
    popStyle();
  }
} // end of draw method

void mouseClicked() {
  if (thinButton.mouseOver()) {
    thickness = 1;
  } else if (thickButton.mouseOver()) {
    thickness = 5;
  }
} // end of mouseClicked method
</code></pre>
<p>Now we have color buttons and they highlight!</p>
<p>All that’s left for this stage is to be able to change the color when we click on a button.</p>
<p>To do this, we need to:</p>
<ol>
<li>Create variables to hold the current color in our paint program. Then, we can set these values to the button’s color.</li>
<li>Draw our line with the new stroke color variables.</li>
<li>Change the stroke color when we click a color button, by adding a loop to the <strong>mouseClicked</strong> method.</li>
</ol>
<h4 id="paintprogram-1">PaintProgram</h4>
<pre><code>int x = 10;
int y = 10;
int w = 50;
int thickness = 1;

Color currentColor;

int r;
int g;
int b;

Button thinButton;
Button thickButton;

// Create an array of 8 Buttons
Button[] colorButtons = new Button[8];

void setup() {
  size(600, 600);
  background(255);

  thinButton = new Button(x, y, w, w);
  thickButton = new Button(x, y + w, w, w);

  // Initialize 8 new Buttons and put each in the array
  // Each button gets an offset so it will be below the previous button
  for (int i = 0; i &lt; colorButtons.length; i++) {
    int offset = y + (w * (i+ 2));
    colorButtons[i] = new Button (x, y + offset, w, w);
  }

  colorButtons[0].setColor(255, 0, 0);
  colorButtons[1].setColor(0, 0, 255);
  colorButtons[2].setColor(0, 255, 0);
  colorButtons[3].setColor(255, 255, 0);
  colorButtons[4].setColor(255, 0, 255);
  colorButtons[5].setColor(0, 255, 255);
  colorButtons[6].setColor(102, 51, 0);
  colorButtons[7].setColor(0, 0, 0);
} // end of setup method

void draw() {
  thinButton.show();
  thickButton.show();

  // Draw dots for thin and thick buttons
  pushStyle();
  strokeWeight(3);
  point(x + w/2, y + w/2);

  strokeWeight(10);
  point(x + w/2, y + w + w/2);
  popStyle();

  // show each of the color buttons
  for (Button button : colorButtons) {
    button.show();
  }

  if (mousePressed) {
    pushStyle();
    strokeWeight(thickness);
    stroke(r, g, b);
    line(pmouseX, pmouseY, mouseX, mouseY);
    popStyle();
  }
} // end of draw method

void mouseClicked() {
  if (thinButton.mouseOver()) {
    thickness = 1;
  } else if (thickButton.mouseOver()) {
    thickness = 5;
  }

  for (Button button : colorButtons) {
    if (button.mouseOver()) {
      currentColor = button.getColor();
      setColor();
    }
  }
} // end of mouseClicked method

// Having this code in a separate method makes things cleaner.
void setColor() {
  r = currentColor.r;
  g = currentColor.g;
  b = currentColor. b;
}
</code></pre>
<blockquote>
<p>Written with <a href="https://stackedit.io/">StackEdit</a>.</p>
</blockquote>

