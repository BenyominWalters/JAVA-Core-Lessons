---


---

<h1 id="paint-program-part-1">Paint Program Part 1</h1>
<h3 id="paint-program-step-by-step">Paint program step-by-step!</h3>
<p>Because this is a more complex challenge, we’ll walk through it step by step.</p>
<p>Create a new Processing sketch called PaintProgram, or something similar.</p>
<p>Now, let’s use what we learned about mouse detection to create a button class. Create a new tab named Button. Inside create a new Button class. Give the button and x, y, width and height. like so:</p>
<pre><code>class Button {
    int myX;
	int myY;
	int myWidth;
	int myHeight;
}
</code></pre>
<p>Now we have all of the data we need to draw a rectangular button. Let’s add the constructor for our class, and a show method that draws a rectangle.</p>
<pre><code>class Button {
    int myX;
	int myY;
	int myWidth;
	int myHeight;

	Button(int x, int y, int w, int h) {
		myX = x;
	    myY = y;
	    myWidth = w;
	    myHeight = h;
	}
	
	void show() {
	   rect(myX, myY, myWidth, myHeight);
	}
}
</code></pre>
<p>Now if you can add a Button to your program and show it.</p>
<h4 id="paintprogram">PaintProgram</h4>
<p>Button button;<br>
void setup() {<br>
size(600, 600);<br>
background(255);<br>
button = new Button(10, 10, 50, 50);<br>
}</p>
<p>void draw() {<br>
button.show();<br>
}</p>
<p>Run the program and you should see a square in the top left of the screen. That’s a lot of code to draw a square! But our investment will pay off in time. Let’s keep going.</p>
<h3 id="making-stroke-size-buttons">Making Stroke Size Buttons</h3>
<p>Let’s make two buttons to adjust our stroke size. First, instead of hard-coding the button values, let’s make them variables. This way it will be easier to add more buttons to the screen in alignment.</p>
<p>Then, we’ll make two buttons, one for a thin stroke and one for a thick stroke.</p>
<pre><code>int x = 10;
int y = 10;
int w = 50;

Button thinButton;
Button thickButton;

void setup() {
  size(600, 600);
  background(255);
  
  thinButton = new Button(x, y, w, w);
  thickButton = new Button(x, y + w, w, w);
}

void draw() {
  thinButton.show();
  thickButton.show();
}
</code></pre>
<p>Because we created a Button class, we were able to add new buttons easily. Now, let’s move into our Button class and add some functionality. These changes will automatically affect all of our buttons without breaking any of our current code.</p>
<p>Let’s use the <strong>mouseOver</strong> method that we wrote previously so that our buttons can detect the mouse.</p>
<p>Then, we’ll add a highlight method that will change the button color. This bit of interactivity will make our program more responsive and user-friendly.</p>
<h4 id="button">Button</h4>
<pre><code>class Button {
  int myX;
  int myY;
  int myWidth;
  int myHeight;

  Button(int x, int y, int w, int h) {
    myX = x;
    myY = y;
    myWidth = w;
    myHeight = h;
  }

  void show() {
    highlight(); // this new method checks if a button should be highlighted
    rect(myX, myY, myWidth, myHeight);
  }
	
	This method highlights a button if the mouse hovers over it
  void highlight() {
    if (mouseOver()) {
      fill(150, 150, 150);
    } else {
      fill(255, 255, 255);
    }
  }
	
	// This method detects the mouse	
  boolean mouseOver() {
    boolean over;
    if (mouseX &gt; myX &amp;&amp; mouseX &lt; myX + myWidth &amp;&amp; mouseY &gt; myY &amp;&amp; mouseY &lt; myY + myHeight) {
      over = true;
    } else {
      over = false;
    }
    return over;
  }
}
</code></pre>
<p>Great! Now we have an interactive button that knows when the mouse is hovering over it. We can use this check if this button is clicked. Back to the Paint Program.</p>
<p>Let’s add code to draw when the mouse is pressed. We’ll control the thickness of our line with our buttons.</p>
<p>We need to add three things to our program:</p>
<ol>
<li>A variable to hold the thickness value.</li>
<li>An if statement that will draw a line when the mouse is pressed.</li>
<li>A mouseClicked method (predefined in Processing) that checks if we clicked the thin or the thick button and sets the thickness accordingly.</li>
</ol>
<p>While we’re at it, let’s also draw a dot to represent the thickness over our buttons, so we can tell what they do.</p>
<p>Here is the code.</p>
<h4 id="paintprogram-1">PaintProgram</h4>
<pre><code>int x = 10;
int y = 10;
int w = 50;
int thickness = 1;

Button thinButton;
Button thickButton;

void setup() {
  size(600, 600);
  background(255);
  
  thinButton = new Button(x, y, w, w);
  thickButton = new Button(x, y + w, w, w);
} // end of setup method

void draw() {
  thinButton.show();
  thickButton.show();
	
  // Draw dots for thin and thick buttons
  pushStyle();
  strokeWeight(3);
  point(x + w/2, y + w/2);
  strokeWeight(10);
  point(x + w/2, y + w + w/2);
  popStyle();
  
  if (mousePressed) {
    pushStyle();
    strokeWeight(thickness);
    line(pmouseX, pmouseY, mouseX, mouseY);
    popStyle();
  }
} // end of draw method

void mouseClicked() {
  if (thinButton.mouseOver()) {
    thickness = 1;
  } else if (thickButton.mouseOver()) {
    thickness = 5;
  }
} // end of mouseClicked method
</code></pre>
<p>We now have a simple drawing program that uses buttons to make it interactive! In the next section, we’ll add color buttons to change the color of our lines.</p>
<blockquote>
<p>Written with <a href="https://stackedit.io/">StackEdit</a>.</p>
</blockquote>

